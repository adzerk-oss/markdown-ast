<S>                     = BLOCKS

BLOCKS                  = (<BLANK-LINE> | BLOCK)+
<BLOCK>                 = TEXT-BLOCK | TAGGED-BLOCK

TEXT-BLOCK              = TEXT-BLOCK-LINE+
<TEXT-BLOCK-LINE>       = (!TAGGED-BLOCK-TAG LINE | <BLANK-LINE>)+

TAGGED-BLOCK            = !ESCAPED TAGGED-BLOCK-TAG TAGGED-BLOCK-BODY
TAGGED-BLOCK-TAG        = TAGGED-BLOCK-TAG-IN CLJ?
TAGGED-BLOCK-BODY       = TAGGED-BODY-START INDENTED-LINE*
<TAGGED-BODY-START>     = <EOL> | <SP>+ <EOL> | <SP>+ NONBLANK-LINE

<INDENTED-LINE>         = BLANK-LINE | <INDENT> NONBLANK-LINE
<INDENT>                = SP SP

LINE                    = BLANK-LINE | NONBLANK-LINE
NONBLANK-LINE           = !BLANK-LINE LINE-CHAR+ EOL
BLANK-LINE              = SP* EOL
<LINE-CHAR>             = !EOL ANY

CLJ                     = MAP | VEC
MAP                     = LCB (VEC | MAP | STRING | NON-PUNCT)* RCB
VEC                     = LSB (VEC | MAP | STRING | NON-PUNCT)* RSB

STRING                  = DQ (STRING-CHAR | ESCAPED)* DQ
<STRING-CHAR>           = !(ESC | DQ) ANY

<NON-PUNCT>             = !PUNCT ANY
<PUNCT>                 = DQ | LSB | RSB | LCB | RCB

<ESCAPED>               = ESC ANY

<SP>                    = " "
<EOL>                   = "\n"
<LSB>                   = '['
<RSB>                   = ']'
<LCB>                   = '{'
<RCB>                   = '}'
<DQ>                    = '"'
<ESC>                   = "\\"
<ANY>                   = #"."
