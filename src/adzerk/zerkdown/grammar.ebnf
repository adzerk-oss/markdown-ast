<S>                     = BLOCKS

BLOCKS                  = (<BLANK-LINE> | BLOCK)+
<BLOCK>                 = TEXT-BLOCK | TAGGED-BLOCK

TEXT-BLOCK              = TEXT-BLOCK-LINE+
<TEXT-BLOCK-LINE>       = (!TAGGED-BLOCK-TAG LINE | <BLANK-LINE>)+

TAGGED-BLOCK            = !ESCAPED-CHAR TAGGED-BLOCK-TAG TAGGED-BLOCK-BODY
TAGGED-BLOCK-TAG        = TAGGED-BLOCK-TAG-IN CLJ?
TAGGED-BLOCK-BODY       = TAGGED-BODY-START INDENTED-LINE*
<TAGGED-BODY-START>     = <EOL> | <SP>+ <EOL> | <SP>+ NONBLANK-LINE

<INDENTED-LINE>         = BLANK-LINE | <INDENT> NONBLANK-LINE
<INDENT>                = SP SP

LINE                    = BLANK-LINE | NONBLANK-LINE
NONBLANK-LINE           = !BLANK-LINE LINE-CHAR+ EOL
BLANK-LINE              = SP* EOL
<SP>                    = " "
<EOL>                   = "\n"
<LINE-CHAR>             = !EOL ANY-CHAR

CLJ                     = MAP | VEC
MAP                     = LCB (VEC | MAP | STRING | NON-PUNCT)* RCB
VEC                     = LSB (VEC | MAP | STRING | NON-PUNCT)* RSB

STRING                  = DQ (STRING-CHAR | ESCAPED-CHAR)* DQ
<STRING-CHAR>           = !(ESCAPE-CHAR | DQ) ANY-CHAR

<NON-PUNCT>             = !PUNCT ANY-CHAR
<PUNCT>                 = DQ | LSB | RSB | LCB | RCB
<LSB>                   = '['
<RSB>                   = ']'
<LCB>                   = '{'
<RCB>                   = '}'
<DQ>                    = '"'

<ANY-CHAR>              = #"."
<ESCAPE-CHAR>           = "\\"
<ESCAPED-CHAR>          = ESCAPE-CHAR ANY-CHAR
